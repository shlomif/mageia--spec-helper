#!/usr/bin/perl
# $Id$
# Strip files

use strict;
use warnings;
use File::Find;

my $buildroot = $ENV{RPM_BUILD_ROOT};
die "No build root defined" unless $buildroot;
die "Invalid build root" unless -d $buildroot;
# normalize build root
$buildroot =~ s|/$||;

my $exclude_pattern = join('|',
    map { '(:?' . quotemeta($_) . ')' }
    $ENV{EXCLUDE_FROM_STRIP} ?
        split(' ', $ENV{EXCLUDE_FROM_STRIP}) : (),
    '/usr/lib/debug'
);
$exclude_pattern = qr/$exclude_pattern/;

my (@shared_libs, @executables, @static_libs);
find(\&testfile, $buildroot);

system(
    "strip",
    "--remove-section=.comment",
    "--remove-section=.note",
    $_) foreach @shared_libs, @executables;

# TODO: we should write a binding for libfile...
sub expensive_test {
    my ($file) = @_;
    my $type = `file -- $file`;
}

# Check if a file is an elf binary, shared library, or static library,
# for use by File::Find. It'll fill the following 3 arrays with anything
# it finds:
sub testfile() {
    # skip symlinks
    return if -l $_;
    # skip directories
    return if -d $_;
    # skip excluded files
    return if $File::Find::name =~ $exclude_pattern;

    # Does its filename look like a shared library?
    if (m/\.so/) {
        # Ok, do the expensive test.
        if (expensive_test($_) =~ m/ELF.*shared/) {
            push @shared_libs, $File::Find::name;
            return;
        }
    }
    
    # Is it executable? -x isn't good enough, so we need to use stat.
    my (undef, undef, $mode, undef) = stat(_);
    if ($mode & 0111) {
        # Ok, expensive test.
        if (expensive_test($_) =~ m/ELF.*executable/) {
            push @executables, $File::Find::name;
            return;
        }
    }
    
    # Is it a static library, and not a debug library?
    if (m/lib.*\.a/ && ! m/_g\.a/) {
        push @static_libs, $File::Find::name;
        return;
    }
}
